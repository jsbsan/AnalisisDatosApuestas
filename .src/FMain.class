' Gambas class file

Public listaCuenta As New ClaseCuenta[]
Public ListaId As New ClaseGestorIdBeneficio

Public GlobalEnJuego As Float
Public GlobalTotal As Float
Public GlogbalCajaPayPalBene As Float

Public ContenidoFichero As String

Public Sub Form_Open()

End

Public Sub Form_Close()

    settings.write(Me)

End

Public Sub Form_Show()

    settings.read(Me)

    'hago click en click analizar
    ButtonAnalizar_Click()
    'y luego en ordenar por fecha
    ButtonPorFecha_Click()

End

Public Sub ButtonAnalizar_Click()
    '' id fichero	fecha	Cuenta	Saldo	comentario	fuente	VARIOS

    Dim contenido As String[]
    Dim l As String
    Dim dato As String[]
    Dim nombrecsv As String = User.home & "/informacion.csv" ''indicar nombre del fichro que contiene los datos
    Dim valor As Float
    Dim total As Float
    'crea lista de cuentas de suma...
    Dim tmpCuenta As ClaseCuenta

    'borrado de datos anteriore...
    listaCuenta = New ClaseCuenta[]

    ListaId = New ClaseGestorIdBeneficio

    If Not Exist(nombrecsv) Then
        Message.Error("No existe fichero de datos")
        Quit
    Endif

    'crea las cuentas partiendo del fichero de datos CuentasM.txt
    contenido = Split(File.Load("CuentasM.txt"), "\n")
    For Each l In contenido
        tmpcuenta = New ClaseCuenta
        tmpcuenta.nombre = l
        tmpcuenta.suma = 0
        listaCuenta.Add(tmpCuenta)
        tmpCuenta = Null
    Next

    'leer fichero de datos...

    ContenidoFichero = File.Load(nombrecsv)

    contenido = Split(Upper(ContenidoFichero), "\n")
    contenido.Delete(0) ' la primera fila contiene los nombre de los campos

    'borrado de datos que esten en los textAreas
    TextArea1.text = ""
    TextAreaResumen.text = ""

    For Each l In contenido

        dato = Split(l, gb.Tab)

        'el dato de la cuenta esta en 2 columna y saldo en la 3
        Try valor = Val(Replace(Replace(dato[3], ".", ","), " €", ""))
        If Error Then

        Else
            '-------------------------------------------------
            'tabla de datos Cuenta / Saldo
            '-------------------------------------------------
            sumasi(dato[2], valor) 'cuenta y saldo

            '-------------------------------------------------
            'tabla de datos ID / Zzbeneficio
            '-------------------------------------------------
            If dato[2] = "ZZ BENEFICIO" Then
                ListaId.agrega(dato[0], VALOR, dato[1])
            Endif

            '-------------------------------------------------
            'tabla de datos ID / Zzbeneficio /enjuego
            '-------------------------------------------------
            If comun.contiene(dato[2], "ZEN ") Then
                ListaId.agregaInversion(dato[0], VALOR)
            Endif

        Endif

    Next
    ' ya he acabado de contar las cuentas y sus sumas
    ' Print "------------------------------------"
    ' Print "--        Sumas de cuentas         -"
    ' Print "------------------------------------"
    For Each tmpcuenta In ListaCuenta
        'Print tmpCuenta.nombre, Format(Round(tmpcuenta.suma, -2), "#.##")
        If Round(tmpcuenta.suma, -2) <> 0 Then

            total += Round(tmpcuenta.suma, -2)
            TextArea1.text &= cambiaCeros(Format(Round(tmpcuenta.suma, -2), "0####.#0")) & gb.tab & tmpCuenta.nombre & "\n"
        Endif
    Next

    TextArea1.text &= "____________________________\n"
    TextArea1.text &= "Total:" & Format(Round(total, -2), "0###.#0")

    'Calculo de sumas de Banco / Casa Apuestas / En Juego  / Zz Beneficio
    Dim enbanco As Float
    Dim enPAYPAL As Float
    Dim enzen As Float
    Dim enBeneficioEsperado As Float
    Dim enCasas As Float
    For Each tmpcuenta In ListaCuenta

        If comun.contiene(tmpCuenta.nombre, "PAYPAL") Then
            enPAYPAL += Round(tmpCuenta.suma, -2)
            Continue
        Endif

        If comun.contiene(tmpCuenta.nombre, "0 ") Then
            enbanco += Round(tmpCuenta.suma, -2)
            Continue
        Endif

        If comun.contiene(tmpCuenta.nombre, "ZEN ") Then
            enZEN += Round(tmpCuenta.suma, -2)
            Continue
        Endif
        If comun.contiene(tmpCuenta.nombre, "ZZ ") Then
            enBeneficioEsperado += Round(tmpCuenta.suma, -2)
            Continue
        Endif
        enCasas += Round(tmpCuenta.suma, -2)

    Next
    '--------------------------------------
    'Tabla 2:resultados...
    '--------------------------------------
    Dim TotalesRecalculo As Float = Round(enbanco + enPAYPAL + enZEn + enBeneficioEsperado + enCasas, -2)

    TextAreaResumen.text &= "En Banco:         " & gb.tab & cambiaCeros(Format(Round(enbanco, -2), "0####.#0")) & gb.tab & Porcentaje(enbanco, TotalesRecalculo) & "\n"

    TextAreaResumen.text &= "En Paypal:        " & gb.tab & cambiaCeros(Format(Round(enPAYPAL, -2), "0####.#0")) & gb.tab & Porcentaje(enPAYPAL, TotalesRecalculo) & "\n"

    TextAreaResumen.text &= "En Casas:         " & gb.tab & cambiaCeros(Format(Round(enCasas, -2), "0####.#0")) & gb.tab & Porcentaje(enCasas, TotalesRecalculo) & "\n"

    TextAreaResumen.text &= "En Juego:         " & gb.tab & cambiaCeros(Format(Round(enZEn, -2), "0####.#0")) & gb.tab & Porcentaje(enZEN, TotalesRecalculo) & "\n"
    TextAreaResumen.text &= "En Beneficio Esp.:" & gb.tab & cambiaCeros(Format(Round(enBeneficioEsperado, -2), "0####.#0")) & gb.tab & Porcentaje(enBeneficioEsperado, TotalesRecalculo) & "\n"
    TextAreaResumen.text &= "__________________" & "\n"
    TextAreaResumen.text &= "Total: " & gb.tab & Str$(Round(totalesRecalculo, -2))

    ProgressBarEnJuego.value = enzen / totalesRecalculo

    ProgressBarBenef.value = enBeneficioEsperado / totalesRecalculo

    ProgressBarCasas.value = enCasas / totalesRecalculo

    ProgressBarPaypal.value = enPAYPAL / totalesRecalculo

    ProgressBarBanco.value = enbanco / totalesRecalculo

    GlobalEnJuego = Round(enzen, -2)
    GlobalTotal = Round(totalesRecalculo, -2)
    GlogbalCajaPayPalBene = GlobalTotal - GlobalEnJuego

    '--------------------------------------
    ' TABLA DE DATOS DE ID -> mejor por fecha
    '--------------------------------------
    ButtonPorFecha_Click()
    'TextAreaEnJuego.TEXT = ListaId.listar()

End

Public Sub Porcentaje(valor As Float, total As Float) As String

    Return Str$(Round(valor * 100 / total, -2)) & " %"

End

Public Sub sumasi(textocuenta As String, valor As Float)

    Dim t As ClaseCuenta

    For Each t In listaCuenta

        If t.nombre = textocuenta Then
            t.suma += valor
            ' If t.nombre = "WILLIAMHILL" Then
            '     Print "WILLIAMHILL", T.SUMA
            ' Endif
        Endif
    Next

End

Public Function cambiaCeros(dato As String) As String

    If Mid$(dato, 1, 3) = "000" Then Return "   " & Mid$(dato, 4, 10)
    If Mid$(dato, 1, 2) = "00" Then Return "  " & Mid$(dato, 3, 10)
    If Mid$(dato, 1, 1) = "0" Then Return " " & Mid$(dato, 2, 10)

    If Mid$(dato, 1, 4) = "-000" Then Return "  -" & Mid$(dato, 5, 10)
    If Mid$(dato, 1, 3) = "-00" Then Return " -" & Mid$(dato, 4, 10)
    If Mid$(dato, 1, 2) = "-0" Then Return "-" & Mid$(dato, 3, 10)

End

Public Sub ButtonEditorPluma_Click()

    Exec ["pluma", User.home & "/informacion.csv"]

End

Public Sub ButtonPorFecha_Click()

    TextAreaEnJuego.text = ListaId.listarOrdenFecha(GlobalEnJuego, GlogbalCajaPayPalBene, GlobalTotal)
    ButtonGraficar_Click()

End

Public Sub ButtonNormalListadoApuestanPendientes_Click()

    '--------------------------------------
    ' TABLA DE DATOS DE ID
    '--------------------------------------
    TextAreaEnJuego.TEXT = ListaId.listar()

End

Public Sub ButtonPorId_Click()

    '--------------------------------------
    ' TABLA DE DATOS DE ID
    '--------------------------------------
    TextAreaEnJuego.TEXT = ListaId.listarOrdenID()

End

Public Sub ButtonFiltrar_Click()

    Dim mf As New FormFiltro
    Dim tmplista As New String[]

    If ContenidoFichero <> "" Then
        tmplista.Add(ContenidoFichero)
        mf.listado = tmplista
        mf.Show()
    Else
        Message.Info("no hay cargada información")
    Endif

End

Public Sub ButtonCopiaResumen_Click()

    Clipboard.Copy(TextArea1.Text)

End

Public Sub ButtonCopiaResumenCompactado_Click()

    Clipboard.Copy(TextAreaResumen.Text)

End

Public Sub ButtonApuestasEnJuego_Click()

    Clipboard.Copy(TextAreaEnJuego.Text)

End

''TODO: BARRAS DE PROGRESO http://ploticus.sourceforge.net/gallery/propbars1.htm

Public Sub ButtonGraficar_Click()

    Dim p As Pie
    Dim a As Integer
    Dim tmpAreaDrawing As DrawingArea

    Dim child As Control
    Dim fechaanterior As String = ""
    Dim contador As Integer = 0

    For Each child In HPanelGrafica.Children
        Child.Delete()
    Next

    For a = 0 To ListaId.listagrafica.count - 1
        '-----------------------------
        'añadir grafico
        '-----------------------------
        tmpAreaDrawing = New DrawingArea(HPanelGrafica) As "grupoGrafico"

        tmpAreaDrawing.w = 50
        tmpAreaDrawing.h = 50
        tmpAreaDrawing.Tooltip = ListaId.listagrafica[a].texto
        p = New Pie(tmpAreaDrawing, ListaId.listagrafica[a].enjuego, ListaId.listagrafica[a].caja, 0)
        If ListaId.listagrafica[a].fecha = fechaanterior Then
            p.colorfondo = contador
        Else
            contador += 1
            p.colorfondo = contador
            fechaanterior = ListaId.listagrafica[a].fecha

        Endif

        tmpAreaDrawing.tag = p
    Next

End

Public Sub grupoGrafico_Draw()

    Last.tag.dibujar()

End
